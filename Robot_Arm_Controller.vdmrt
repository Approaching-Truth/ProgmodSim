class Robot_Arm_Controller
    instance variables
        public ctl_joint_angle: real;
        public ctl_target_joint_angle: real;
        public previous_angle_error: real;
        public ctl_angular_velocity: real;
        public ctl_angular_acceleration: real;
        public acceptance_range: real;
        public state_history: seq of (real * real * real);
        private last_time: real;
        private K_p : real;
        private K_d : real;
        public ctl_mode : <Initialisation> | <Normal> | <Emergency>;
    
    values
        update_rate : real = 5E8;

    operations
        -- Constructor
        public Robot_Arm_Controller: () ==> Robot_Arm_Controller
        Robot_Arm_Controller() == (
            ctl_target_joint_angle := -1; -- Will be overwritten
            ctl_joint_angle := 0;
            previous_angle_error := 0;
            ctl_angular_velocity := 0;
            ctl_angular_acceleration := 0;
            acceptance_range := 0.01;
            state_history := [];
            last_time := time/1E9;
            K_p := 0.1;
            K_d := 0.1;
            ctl_mode := <Initialisation>;
        );

        public set_controller_params: real * real ==> ()
        set_controller_params (K_p_in, K_d_in) == (
            K_p := K_p_in;
            K_d := K_d_in;
        );


        public record_state: () ==> ()
        record_state() == duration(0)(
            state_history := state_history ^ [mk_(time, ctl_joint_angle, ctl_angular_velocity)]
        );
        
        public find_target: () ==> ()
        find_target() == duration (5E8) (
            dcl target : real;
            dcl angle_error : real;
            dcl delta_angle_error : real;
            dcl new_torque : real;
            dcl delta_t : real;
            dcl t : real;
            
            -- Set timing local variabels
            t := time/1E9;
            delta_t := t - last_time;
            last_time := t;

            -- Get target from UI
            target := ctl_target_joint_angle;

            -- Emergency break off 
            MySystem`environment.emergency_break :=0;
            
            -- Calculate errror
            angle_error := target - ctl_joint_angle;
            delta_angle_error := (angle_error - previous_angle_error) / delta_t;
            
            /*
             -- In accepted range
            if ((angle_error < acceptance_range/2) and (angle_error > -acceptance_range/2)) then (
                -- set stop mode
                skip;
            )
            -- Not in accepted range
            else (
            );
            */
           
            new_torque := K_p * angle_error + K_d * delta_angle_error - 0.5 * ctl_angular_velocity;
            
            MySystem`motor.set_torque(new_torque);

            previous_angle_error := angle_error;
            record_state();
        );
        
        -- Sense joint angle
        public monitor_joint_angle: () ==> ()
        monitor_joint_angle() == duration(1E8) (
            dcl env_angle: real := MySystem`sensor.get_snr_joint_angle();
            dcl env_angular_velocity: real := MySystem`sensor.get_snr_angular_velocty();
            ctl_joint_angle := env_angle;
            ctl_angular_velocity := env_angular_velocity;
            record_state();
        );

        -- Update target
        public update_target: () ==> ()
        update_target () == duration(0)(
            if not (MySystem`ui.get_target_joint_angle() = ctl_target_joint_angle) then (
                ctl_target_joint_angle := MySystem`ui.get_target_joint_angle()
            );
        );

        -- Applies emergency break which stops all motion!
        -- NOTE: This is a quick fix, and will be changed to later iterations!
        public emergency_break: () ==> ()
        emergency_break() == duration(0)(
            -- Set env var to 0
            MySystem`environment.env_angular_acceleration := 0;
            MySystem`environment.env_angular_velocity := 0;

            -- Set ctl var to 0
            ctl_angular_acceleration := 0;
            ctl_angular_velocity := 0;

            -- Set motor torque to 0
            MySystem`environment.emergency_break := -MySystem`motor.get_torque();
        ); 

        private loop: () ==> ()
        
        loop() == duration(0) (
            -- Get current joint angle
            monitor_joint_angle();

            -- Check if target have changed
            update_target();

            -- Calculate new torque
            find_target();

            -- Check and switch mode
            if (    ctl_joint_angle >= MySystem`environment.safe_lb 
                and ctl_joint_angle <= MySystem`environment.safe_ub 
                and MySystem`motor.get_operation_status() = <Working>
                and MySystem`motion_sensor.get_motion_sensor_status() = <Clear>
                ) 
            then (
                ctl_mode := <Normal>;
            ) else (
                ctl_mode := <Emergency>;
            );

            -- Peform action based on mode 

            -- Initialisation
            -- If in Initialisation, do nothing
            if (ctl_mode = <Initialisation>) then (
                skip;
            );
            
            -- Normal
            -- Find target 
            if (ctl_mode = <Normal>) then (
                find_target();
            );
            
            -- Emergency
            -- Stop movement
            if (ctl_mode = <Emergency>) then (
                emergency_break();
            );
        )

        thread
            periodic(5E9, 0, 0, 0)(loop);


end Robot_Arm_Controller